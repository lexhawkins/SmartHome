Model:
    /* Needs at least ONE place defined */
    places+=Place
;

Place:
    'place' name=ID':' 
        (locations+=PlaceLocation
        | rules+=RuleDefinition
        | scenes+=SceneDefinition
        )*
    'end'
;

PlaceLocation:
    'location' name=ID':' 
        (devices+=DeviceDefinition)* 
    'end'
;

DeviceDefinition:
    'device' name=ID':' type=DeviceType
;

DeviceType:
      'AC' 
    | 'Alarm'
    | 'Camera' 
    | 'Light' 
    | 'Lock' 
    | 'Sensor' 
    | 'SmartSpeaker' 
    | 'Thermostat' 
;

RuleDefinition:
    'rule' name=STRING ':' 
        condition_block=ConditionBlock
        action_block=ActionBlock
    'end'
;

SceneDefinition:
    'scene' name=STRING 'at' location=[PlaceLocation] ':' 
        action_block=ActionBlock
    'end'
;

ConditionBlock:
    'if' condition=Condition
;

ActionBlock:
    ('do' actions+=Action)+
;

Condition:
        DetectorCondition
;

DetectorCondition:
    device=[DeviceDefinition] 'detects' functionality=RuleFunctionalities (op=ComparisonOperator)? (value=INT)?
;

Value:
     INT
   | STRING
;

RuleFunctionalities:
      'temperature'
    | 'movement'
    | 'sound'
    | 'light'
;


Action:
    device=[DeviceDefinition] command=CommandAction (value=Value)?
;

CommandAction:
      'turn_on'
    | 'turn_off'
    | 'lock'
    | 'unlock'
    | 'set_to_temperature' 
    | 'play_music'
    | 'announce'
    | 'activate'
    | 'deactivate'
    | 'record'
    | 'stop'
    | 'send_alert'
;


ComparisonOperator: '>' | '<' | '=';

Comment:
      /\/\/.*$/ 
    | /\/\*.*?\*\//
;
